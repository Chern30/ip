plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'org.openjfx.javafxplugin' version '0.0.13'
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.10.0'
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.10.0'
}

test {
    useJUnitPlatform()

    testLogging {
        events "passed", "skipped", "failed"

        showExceptions true
        exceptionFormat "full"
        showCauses true
        showStackTraces true
        showStandardStreams = false
    }
}

sourceSets {
    main {
        resources {
            srcDirs = ['src/main/resource']
        }
    }
}

application {
    mainClass.set("blub.blub")
}

def linuxJfx = configurations.detachedConfiguration(
    dependencies.create("org.openjfx:javafx-base:17:linux"),
    dependencies.create("org.openjfx:javafx-controls:17:linux"),
    dependencies.create("org.openjfx:javafx-fxml:17:linux"),
    dependencies.create("org.openjfx:javafx-graphics:17:linux")
)

shadowJar {
    archiveBaseName = "blub"
    archiveClassifier = null
    from linuxJfx.collect { zipTree(it) }
}

run{
    standardInput = System.in
}

javafx {
    version = "17"
    modules = [ 'javafx.controls', 'javafx.fxml' ]
}